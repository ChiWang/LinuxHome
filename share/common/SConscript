#=====================================================================
#   File:   SConscript
#   Author: Chi WANG  (chiwang@mail.ustc.edu.cn)    24/07/2013
#---------------------------------------------------------------------
#   Description:
#
#   1.  Root
#       ARGUMENTS.get('root',value):
#           value = 1,      use root
#           scons root=0,   not use
#   2. RooFit
#         ARGUMENTS.get('roofit',value):
#          value = 0,       not use roofit
#          scons roofit=1   use roofit
#   3.  Geant4
#         ARGUMENTS.get('g4',value):
#           value = 0,      not use geant4
#           scons g4=1,     use geant4
#   4.  OpenMP
#         ARGUMENTS.get('omp',value):
#           value = 0,      not use OpenMP
#           scons omp=1,    use it
#---------------------------------------------------------------------
#   History:
#                           Last update:  13/12/2013   14:04:02
#=====================================================================

#import sys
import os
env = Environment(ENV = os.environ)

#   set the programme name
ExeName='MyX'

#   set debug modle.
#    if yes, then can use gdb:  http://www.ibm.com/developerworks/cn/linux/sdk/gdb/
if int(ARGUMENTS.get('debug',1)):
    env.Prepend(CCFLAGS = '-g')
    env.Append(CCFLAGS = '-DDEBUG')
else:
    env.Append(CCFLAGS = '-DRELEASE')

#   set Root environment
if int(ARGUMENTS.get('root',0)):
    env.ParseConfig("root-config --cflags --libs")
    if int(ARGUMENTS.get('roofit',0)):
        env.Append(LIBS=['RooFitCore','RooFit'])

#   set Geant4 environment
if int(ARGUMENTS.get('g4',0)):
    g4sys=os.environ['G4INSTALL']
    env.PrependENVPath('PATH', g4sys+'/../../../bin')
    env.ParseConfig("geant4-config --cflags --libs")

#   set OpenMP
if int(ARGUMENTS.get('omp',0)):
    ExeName=ExeName+"_omp"
    env.MergeFlags('-fopenmp')

#   include
env.Append(CPPPATH = '../include')

#   use external library
#env.Append(LIBS=['XXX','xxxx2'])
#env.Append(LIBPATH=['xxxx'])

#   the following 3 lines for checking your setted value
#for item in sorted(env.Dictionary().items()):
#  print "V = '%s',\t\tValue = '%s'" % item
#Exit(1)

#   main build
env.Program('../'+ExeName,Glob("*.cc"))
#env.SharedLibrary('../'+ExeName,Glob("*.cc"))

